#!/usr/bin/env fish

# This script changes focus to the space given as an argument. The macOS
# `Switch to Desktop` commands do not switch focus if the space is already
# visible on a display. To workaround this issue, we use `yabai` to focus the
# display instead, effectively moving focus to the wanted space.

set --local space_index $argv[1]
set --local space (yabai --message query --spaces | jq --argjson index $space_index '.[] | select(.index == $index)')

if test (echo $space | jq '."is-visible"') = true -a (echo $space | jq '."has-focus"') = false
    set --local display_index (echo $space | jq '.display')
    yabai --message display --focus $display_index
else
    switch-desktop $space_index
end
